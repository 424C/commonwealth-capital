// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

contract Neighbor is ERC721Enumerable, Ownable {
    /**
     * @dev _baseTokenURI for computing {tokenURI}. If set, the resulting URI for each
     * token will be the concatenation of the `baseURI` and the `tokenId`.
     */
    string _baseTokenURI;

    //  Initial price of mint
    uint256 public _price = 0.05 ether;

    // _paused is used to pause the contract in case of an emergency
    bool public _paused;

    // max number of BB tokens
    uint256 public maxTokenIds = 23;

    // total number of tokenIds minted
    uint256 public tokenIds;

    modifier onlyWhenNotPaused() {
        require(!_paused, "Contract currently paused");
        _;
    }

    /**
     * @dev constructor initializes the contract with passed parameters */
    constructor(string memory baseURI) ERC721("Neighbor", "NB") {
        _baseTokenURI = baseURI;
    }

    /**
     * @dev mint allows a user to mint a BB
     */
    function mint() public payable onlyWhenNotPaused {
        require(tokenIds < maxTokenIds, "SUPPLY_LIMIT_REACHED");
        require(msg.value >= _price, "INCORRECT_ETH_AMOUNT");
        tokenIds += 1;
        _safeMint(msg.sender, tokenIds);
    }

    /**
     * @dev _baseURI overides the Openzeppelin's ERC721 implementation which by default
     * returned an empty string for the baseURI
     */
    function _baseURI() internal view virtual override returns (string memory) {
        return _baseTokenURI;
    }

    /**
     * @dev setPaused allows Owner to pause minting
     */
    function setPaused(bool val) public onlyOwner {
        _paused = val;
    }

    /**
     * @dev withdraw sends all the ether in the contract
     * to the owner of the contract
     */
    function withdraw() public onlyOwner {
        address _owner = owner();
        uint256 amount = address(this).balance;
        (bool sent, ) = _owner.call{value: amount}("");
        require(sent, "FAILED_SEND_ETH");
    }

    // Function to receive Ether. msg.data must be empty
    receive() external payable {}

    // Fallback function is called when msg.data is not empty
    fallback() external payable {}
}
